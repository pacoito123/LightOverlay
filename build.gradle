import java.text.SimpleDateFormat

plugins {
    id 'fabric-loom' version '0.2.5-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "LightOverlay"
version = ((String) project.mod_version).contains("unstable") ? (project.mod_version + "." + buildTime()) : project.mod_version

def minecraftVersion = "1.15-pre3"
def yarnVersion = "1.15-pre3+build.2"
def fabricVersion = "0.4.18+build.271-1.15"
def cloth_version = "1.0.1-unstable.201912010433"
def cloth_config_version="2.4-unstable.201911031154"

minecraft {
}

processResources {
    filesMatching('fabric.mod.json') {
        expand 'version': project.version
    }
    inputs.property "version", project.version
}

static def buildTime() {
    def df = new SimpleDateFormat("yyyyMMddHHmm")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

repositories {
    jcenter()
    maven { url "https://tehnut.info/maven/" }
    maven { url "https://minecraft.curseforge.com/api/maven" }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraftVersion}"
    mappings "net.fabricmc:yarn:${yarnVersion}"
    modCompile "net.fabricmc:fabric-loader:0.7.2+build.174"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modCompile "net.fabricmc.fabric-api:fabric-api:${fabricVersion}"
    modCompile("me.shedaniel.cloth:cloth-events:${cloth_version}") {
        transitive = false
    }
    modImplementation("me.shedaniel.cloth:config-2:${cloth_config_version}") {
        transitive = false
    }
    modImplementation("io.github.prospector:modmenu:1.7.13-unstable.19w38b+build.5") {
        transitive = false
    }
    include("me.shedaniel.cloth:cloth-events:${cloth_version}") {
        transitive = false
    }
    include("me.shedaniel.cloth:config-2:${cloth_config_version}") {
        transitive = false
    }
}
